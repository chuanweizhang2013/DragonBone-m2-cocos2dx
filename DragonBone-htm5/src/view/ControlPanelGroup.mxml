<?xml version="1.0" encoding="utf-8"?>
<s:VGroup xmlns:fx="http://ns.adobe.com/mxml/2009" 
		  xmlns:s="library://ns.adobe.com/flex/spark" 
		  xmlns:mx="library://ns.adobe.com/flex/mx" 
		  xmlns:components="view.components.*"
		  width="100%" height="100%" gap="0"
		  creationComplete="this_creationCompleteHandler(event)" xmlns:view="view.*">
	<fx:Script>
		<![CDATA[
			import dragonBones.utils.ConstValues;
			
			import message.Message;
			import message.MessageDispatcher;
			
			import model.ImportDataProxy;
			import model.SettingDataProxy;
			
			import mx.events.DragEvent;
			import mx.events.FlexEvent;
			import mx.events.ListEvent;
			
			import spark.events.GridSelectionEvent;
			import spark.events.IndexChangeEvent;
			
			private static const AT_NAME:String = ConstValues.AT + ConstValues.A_NAME;
			private var _importDataProxy:ImportDataProxy = ImportDataProxy.getInstance();
			
			protected function this_creationCompleteHandler(event:FlexEvent):void
			{
				movementList.dataProvider = _importDataProxy.animationDataProxy.movementsMC;
				movementList.addEventListener(IndexChangeEvent.CHANGE, movementList_onChange);
				
				boneTree.addEventListener(ListEvent.CHANGE, boneHandler);
				boneTree.addEventListener(DragEvent.DRAG_COMPLETE, boneHandler);
				
				MessageDispatcher.addEventListener(MessageDispatcher.CHANGE_ARMATURE_DATA, commandHandler);
				MessageDispatcher.addEventListener(MessageDispatcher.CHANGE_MOVEMENT_DATA, commandHandler);
				MessageDispatcher.addEventListener(MessageDispatcher.CHANGE_BONE_DATA, commandHandler);
				
				MessageDispatcher.addEventListener(MessageDispatcher.UPDATE_BONE_PARENT, commandHandler);		
			}
			
			private function movementList_onChange(e:Event):void
			{
				var movementXML:XML = movementList.selectedItem as XML;
				var movementName:String = movementXML?movementXML.attribute(ConstValues.A_NAME):"";
				_importDataProxy.animationDataProxy.changeMovement(movementName);
			}
			
			private function boneHandler(e:Event):void
			{
				switch(e.type)
				{
					case ListEvent.CHANGE:
						var boneXML:XML = boneTree.selectedItem as XML;
						var boneName:String = boneXML?boneXML.attribute(ConstValues.A_NAME):"";
						_importDataProxy.armatureDataProxy.changeBone(boneName);
						_importDataProxy.animationDataProxy.changeMovementBone(boneName);
						break;
					case DragEvent.DRAG_COMPLETE:
						if(boneTree.lastMoveNode)
						{
							boneName = boneTree.lastMoveNode.attribute(ConstValues.A_NAME);
							var parentXML:XML = boneTree.lastMoveNode.parent();
							var parentName:String = parentXML.attribute(ConstValues.A_NAME);
							if(parentXML.name() != boneTree.lastMoveNode.name())
							{
								parentName = null;
							}
							
							_importDataProxy.armatureDataProxy.updateBoneParent(boneName, parentName);
						}
						break;
				}
			}
			
			private function commandHandler(e:Message):void
			{
				switch(e.type)
				{
					case MessageDispatcher.CHANGE_ARMATURE_DATA:
					case MessageDispatcher.UPDATE_BONE_PARENT:
						boneTree.dataProvider = _importDataProxy.armatureDataProxy.bonesMC;
						boneTree.validateNow();
						for each(var _item:Object in boneTree.dataProvider)
						{
							boneTree.expandChildrenOf(_item, true);
						}
						if(e.type == MessageDispatcher.UPDATE_BONE_PARENT)
						{
							boneTree.selectItemByName(e.parameters[0]);
						}
						break;
					case MessageDispatcher.CHANGE_MOVEMENT_DATA:
						movementList.selectedItem = getListItemByName(movementList, e.parameters[0]);
						break;
					case MessageDispatcher.CHANGE_BONE_DATA:
						boneTree.selectItemByName(e.parameters[0]);
						break;
				}
			}
			
			private function getListItemByName(list:List, itemName:String):XML
			{
				for each(var eachItem:XML in list.dataProvider)
				{
					if(eachItem.attribute(ConstValues.A_NAME) == itemName)
					{
						return eachItem;
					}
				}
				return null;
			}
			
			protected function behaviorAdvancedSwitch_clickHandler(event:MouseEvent):void
			{
				if(behaviorAdvancedControlPanel.width == 0 )
				{
					behaviorAdvancedControlPanel.width = 150;
					behaviorAdvancedSwitch.label = ">>";
				}
				else
				{
					behaviorAdvancedControlPanel.width = 0;
					behaviorAdvancedSwitch.label = "<<";
				}
			}
			
			protected function boneAdvancedSwitch_clickHandler(event:MouseEvent):void
			{
				if(boneAdvancedControlPanel.width == 0 )
				{
					boneAdvancedControlPanel.width = 150;
					boneAdvancedSwitch.label = ">>";
				}
				else
				{
					boneAdvancedControlPanel.width = 0;
					boneAdvancedSwitch.label = "<<";
				}
			}
		]]>
	</fx:Script>
	<fx:Declarations>
	</fx:Declarations>
	<mx:VDividedBox width="100%" height="100%">
		<s:VGroup width="100%" height="100%" gap="-1">
			<s:HGroup width="100%" gap="-1">
				<s:Button width="100%" height="24" label="{resourceManager.getString('resources','behaviorList')}"
						  cornerRadius="0" fontWeight="bold"/>
				<s:Button id="behaviorAdvancedSwitch" width="35" height="24" label="&lt;&lt;"
						  cornerRadius="0" click="behaviorAdvancedSwitch_clickHandler(event)"/>
			</s:HGroup>
			<s:HGroup width="100%" height="100%" gap="-1">
				<s:List id="movementList" minWidth="110" width="100%" height="100%" labelField="{AT_NAME}" borderColor="#000000"/>
				<view:BehaviorAdvancedControlPanel id="behaviorAdvancedControlPanel" width="0" height="100%"/>
			</s:HGroup>
		</s:VGroup>
		<s:VGroup width="100%" height="100%" gap="-1">
			<s:HGroup width="100%" gap="-1">
				<s:Button width="100%" height="24" label="{resourceManager.getString('resources','boneTree')}"
					  cornerRadius="0" fontWeight="bold"/>
				<s:Button id="boneAdvancedSwitch" width="35" height="24" label="&lt;&lt;"
						  cornerRadius="0" click="boneAdvancedSwitch_clickHandler(event)"/>
			</s:HGroup>
			<s:HGroup width="100%" height="100%" gap="-1">
				<components:DragTree id="boneTree" minWidth="110" width="100%" height="100%" dragEnabled="true"
									 dropEnabled="true" labelField="{AT_NAME}" textRollOverColor="#CCCCCC"
									 textSelectedColor="#CCCCCC"/>
				<view:BoneAdvancedControlPanel id="boneAdvancedControlPanel" width="0" height="100%"/>
			</s:HGroup>
		</s:VGroup>
	</mx:VDividedBox>
</s:VGroup>
